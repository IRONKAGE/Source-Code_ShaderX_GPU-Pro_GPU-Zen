#include "LightHelper.h"

cbuffer CameraBuffer : register(b0)
{
	float4x4 viewProjMat;
	float4x4 viewMat;
	float4x4 projMat;
}

struct VSOUT
{
	float3 viewPos : VIEW_POS;
	uint lightID : LIGHTID;
};


StructuredBuffer<SpotLight> lights : register(t0);

VSOUT main(float4 position : POSITION, uint ID : SV_InstanceID)
{

	float3 pos = position.xyz;
	float range = lights[ID].pos.w * 1.025f;

	float XZscale = sin(lights[ID].dir_angle.w)/sin(PI/2.0f - lights[ID].dir_angle.w);

   	pos.xz *= XZscale;

   	pos *= range;

   	if(length(pos) > range)
		pos = normalize(pos) * range;

	 //rotate
	float3 dir = lights[ID].dir_angle.xyz;
	float3 k = normalize(cross(float3(0,-1,0), dir));
	float rot_angle = acos(dot(float3(0,-1,0), dir));

	pos = pos*cos(rot_angle) + cross(k, pos)*sin(rot_angle)+k*dot(k, pos)*(1.0f-cos(rot_angle));

	VSOUT vout;
	float4 worldPos = float4(pos + lights[ID].pos.xyz , 1);
    vout.viewPos = mul(viewMat, worldPos);
	vout.lightID = ID;

	return vout;
}
