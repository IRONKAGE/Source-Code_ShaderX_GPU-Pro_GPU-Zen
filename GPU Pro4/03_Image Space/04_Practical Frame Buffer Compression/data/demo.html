<html xmlns="http://www.w3.org/1999/xhtml" lang="en" xml:lang="en">
<head>
<meta http-equiv="Content-Type" content="application/xhtml+xml; charset=UTF-8" />



<script id="forward.frag" type="x-shader/x-fragment">
    precision highp float;

    varying vec2 vTextureCoord;
    varying vec3 vTransformedNormal;
    varying vec4 vPosition;

    uniform sampler2D uSampler;
	
	uniform int render_mode;
	
	vec3 RGB2YCoCg(vec3 c){
		return vec3( 0.25*c.r+0.5*c.g+0.25*c.b, 0.5*c.r-0.5*c.b +0.5, -0.25*c.r+0.5*c.g-0.25*c.b +0.5);
	}
	
    void main(void) {
        vec4 fragColor = texture2D(uSampler, vTextureCoord);
		
		vec3 norm = normalize(vTransformedNormal);
		vec3 P = -vPosition.xyz/vPosition.w;
		//calculate the lighting from two directional lights
		vec3 lightDir1=vec3(19.2,0,70);
		vec3 lightDir2=vec3(-21.2,0,70);
		
		//diffuse lighting
		vec3 diffuse = vec3(1,0,0) * max(0.0,dot(normalize(lightDir1), norm)) +
					   vec3(0,1,0) * max(0.0,dot(normalize(lightDir2), norm));
		
		//specular lighting
		vec3 H1 = lightDir1 + P;
		vec3 H2 = lightDir2 + P;
		vec3 specular = vec3(1,0,0) * pow(max(0.0,dot(normalize(H1), norm)),64.0) +
						vec3(0,1,0) * pow(max(0.0,dot(normalize(H2), norm)),64.0) ;
		
	
		fragColor.xyz=diffuse*(0.1+fragColor.xyz + 0.55*specular);
		if (render_mode==1){
			//compress the color
			fragColor.rgb = RGB2YCoCg(fragColor.rgb);
			
			vec2 crd = gl_FragCoord.xy;
			//since webgl does not support bitwise operators in GLSL, we don't follow exactly the code in the article
			bool pattern = (mod(crd.x,2.0)==mod(crd.y,2.0));
			fragColor.g = (pattern)?fragColor.b: fragColor.g;
			fragColor.b=0.0;
		}
        gl_FragColor = vec4(fragColor.rgb, 1.0);
    }
</script>

<script id="forward.vert" type="x-shader/x-vertex">
    attribute vec3 aVertexPosition;
    attribute vec3 aVertexNormal;
    attribute vec2 aTextureCoord;
	attribute vec3 g_Tangent;

    uniform mat4 uMVMatrix;
    uniform mat4 uPMatrix;
    uniform mat3 uNMatrix;

    varying vec2 vTextureCoord;
    varying vec3 vTransformedNormal;
    varying vec4 vPosition;
	
    void main(void) {
        vPosition = uMVMatrix * vec4(aVertexPosition, 1.0);
        gl_Position = uPMatrix * vPosition;
        vTextureCoord = aTextureCoord;
        vTransformedNormal = uNMatrix * aVertexNormal;
    }
</script>

<script id="simple.vert" type="x-shader/x-vertex">
    attribute vec3 aVertexPosition;

    void main(void) {
        gl_Position = vec4(aVertexPosition,1.0);
    }
</script>

<script id="simple.frag" type="x-shader/x-fragment">
	precision highp float;
	precision highp vec2;
	
	uniform highp sampler2D uSampler;
	
	uniform int filter_type;
	uniform int render_mode;

	
	vec3 YCoCg2RGB(vec3 c){
		c.y-=0.5;
		c.z-=0.5;
		return vec3(c.r+c.g-c.b, c.r + c.b, c.r - c.g - c.b);
	}
	
	const float THRESH=30./255.;

	float edge_filter(vec2 center, vec2 a0, vec2 a1, vec2 a2, vec2 a3){ 
	  vec4 lum = vec4(a0.x, a1.x , a2.x, a3.x);
	  vec4 w = 1.0-step(THRESH, abs(lum - center.x)); 
	  float W = w.x + w.y + w.z + w.w;
	  //Handle the special case where all the weights are zero.
	  //In HDR scenes it's better to set the chrominance to zero. 
	  //Here we just use the chrominance of the first neighbor.
	  w.x = (W==0.0)? 1.0:w.x;  W = (W==0.0)? 1.0:W;  

	  return (w.x*a0.y+w.y*a1.y+w.z*a2.y+w.w* a3.y)/W;
	}
	
    void main(void) {
		vec2 crd = gl_FragCoord.xy;
		vec2 coords = crd/vec2(1024,512);
		vec4 col = texture2D(uSampler, coords);
		

		float chroma = 0.0;
		//filter_type==0 performs no reconstruction
		
		if(filter_type==1){	
			//nearest reconstruction:
			chroma = texture2D(uSampler, coords + vec2(1.0/1024.0,0.0)).g;
		}
		else if (filter_type==2){
			//bilinear reconstruction:
			chroma = 0.25*(texture2D(uSampler, coords + vec2(1.0/1024.0,0.0)).g +
			 texture2D(uSampler, coords - vec2(1.0/1024.0,0.0)).g +
			 texture2D(uSampler, coords + vec2(0.0,1.0/512.0)).g +
			 texture2D(uSampler, coords - vec2(0.0,1.0/512.0)).g);
		}
		else if (filter_type==3){	
			//edge-directed reconstruction:
			vec2 a0 = texture2D(uSampler, coords + vec2(1.0/1024.0,0.0)).rg;
			vec2 a1 = texture2D(uSampler, coords - vec2(1.0/1024.0,0.0)).rg;
			vec2 a2 = texture2D(uSampler, coords + vec2(0.0,1.0/512.0)).rg;
			vec2 a3 = texture2D(uSampler, coords - vec2(0.0,1.0/512.0)).rg;		
			chroma = edge_filter(col.rg, a0, a1, a2, a3);
		}
		

		if(render_mode==1){
			bool pattern = (mod(crd.x,2.0)==mod(crd.y,2.0));
			col.b=chroma;
			col.rgb = (pattern)?col.rbg:col.rgb;
			col.rgb = YCoCg2RGB(col.rgb);
		}


        gl_FragColor =  col;
    }
</script>
	
</head>

<body>
<div style="position:relative;">
<canvas id="WebGL-Demo" style="border: none;" width="720" height="380"></canvas>
<div style="position:absolute; color:#222; bottom:360px; right:10px;"> <span id="frameRate"  >0.0</span> fps </div>
<div style="position:absolute; bottom:360px; left:5px;">
Mode:
<select id="render_mode">
    <option value="0">Forward - Uncompressed</option>
    <option selected value="1">Forward - Compact YCoCg </option>
</select>
 </div>
<div style="position:absolute; bottom:10px; left:5px;">
Filter: 
<select id="filter">
    <option value="0">None</option>
	<option value="1">Nearest</option>
	<option value="2">Bilinear</option>
    <option selected value="3">Edge-Directed</option>
</select>
</div>
 <div style="position:absolute; bottom:10px; right:10px;">
Animate <input type="checkbox" id="animate" checked /> 
 </div>
 <div style="position:absolute; bottom:30px; right:10px;">
High Chroma Contrast <input type="checkbox" id="contrast" /> 
 </div>
</div>


</body>

</html> 
